plugins {
  id "kz.greetgo.upload-to-maven" version "0.0.5"
  id "kz.greetgo.upload-to-gg-repo" version "0.0.5"
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven'
apply plugin: 'idea'

group = 'kz.greetgo'

sourceCompatibility = 1.8
version = '2.0.1'

def ggRepo = System.getenv('GG_REPO')?.trim()
if (!ggRepo) ggRepo = System.getProperty('GG_REPO')?.trim()

repositories {
  mavenLocal()
  if (ggRepo) maven { url ggRepo }
  mavenCentral()
}

dependencies {
  ext.mongoJavaDriverVersion = '3.8.1'

  compileOnly "org.mongodb:mongo-java-driver:$mongoJavaDriverVersion"
  testCompile "org.mongodb:mongo-java-driver:$mongoJavaDriverVersion"

  testCompile group: 'kz.greetgo.db', name: 'greetgo.db', version: '2.0.0'

  testCompile group: 'kz.greetgo.conf', name: 'greetgo.conf', version: '1.0.0'

  testCompile group: 'log4j', name: 'log4j', version: '1.2.17'
  testCompile group: 'org.postgresql', name: 'postgresql', version: '9.4-1202-jdbc42'

  testCompile 'org.testng:testng:6.5.1'
  testCompile 'org.easytesting:fest-assert-core:2.0M10'

  testCompile group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.1.1'

  if (System.getenv('GG_REPO')?.trim()) {
    testCompile "greetgo.3rd-party:oracle-ojdbc6_g:11.2.0.3"
    testCompile "greetgo.3rd-party:oracle-ucp:11.2.0.3"
  } else {
    testCompile fileTree(dir: 'oracle_driver_jars', include: '*.jar')
  }

}

test.useTestNG()

idea.module.inheritOutputDirs = false
idea.module.outputDir = file("$buildDir/classes/main/")
idea.module.testOutputDir = file("$buildDir/classes/test/")

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
[compileJava, compileTestJava]*.options*.debugOptions*.debugLevel = "source,lines,vars"

sourceSets.main.java.srcDirs = ["src"]
sourceSets.test.java.srcDirs = ["test_src"]
sourceSets.main.resources.srcDirs = ["src_resources"]
sourceSets.test.resources.srcDirs = ["test_resources"]

sourceSets.main.resources {
  srcDirs += sourceSets.main.java.srcDirs
  exclude '**/*.java'
}
sourceSets.test.resources {
  srcDirs += sourceSets.test.java.srcDirs
  exclude '**/*.java'
}

jar {
  manifest {
    attributes 'Implementation-Title': project.name
    attributes 'Implementation-Version': version
    attributes 'Implementation-Vendor': "greetgo!"
    attributes 'Created-By': System.getProperty("user.name")
    attributes 'Created-At': new Date().format("yyyy-MM-dd/HH:mm:ss")
  }
}

uploadToMavenCentral {
  description = 'File Storage Implementation'
  url = 'https://tech.greetgo.kz/test_project.php'
  scm {
    url = 'https://github.com/greetgo/fstorage.git'
  }
  developer {
    id = 'pompei'
    name = 'Evgenij Kolpakov'
    email = 'ekolpakov@greet-go.com'
  }
}
